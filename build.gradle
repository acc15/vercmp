plugins {
    id 'net.researchgate.release' version '2.4.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile("com.github.rshift:vercmp:1.0.0")

    testCompile 'org.testng:testng:6.9.10'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {

                name project.name
                packaging "jar"

                description "Comparator for application versions"
                url "https://github.com/rshift/vercmp"

                scm {
                    connection "scm:git:https://github.com/rshift/vercmp.git"
                    developerConnection "scm:git:https://github.com/rshift/vercmp.git"
                    url "https://github.com/rshift/vercmp"
                }

                licenses {
                    license {
                        name "MIT License"
                        url "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id 'rshift'
                        name 'Vyacheslav Mayorov'
                        email 'utvk@ya.ru'
                    }
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives


test.useTestNG()